mapping = _{ SOI ~ statement ~ (NEWLINE+ ~ statement)* ~ NEWLINE* ~ EOI }

statement = _{ assignment | function | if_statement }

assignment = { target_path ~ "=" ~ query_arithmetic }

if_statement = {
    "if" ~ query_arithmetic ~ "{" ~ NEWLINE* ~
        statement ~
    NEWLINE* ~ "}" ~ ( "else" ~ "{" ~ NEWLINE* ~
        statement ~
    NEWLINE* ~ "}" )?
}

// Paths
path_segment = @{ ('a'..'z' | 'A'..'Z' | "_" | "-" )+ ~ ( "[" ~ ('0'..'9')+ ~ "]" )? }

target_path = @{ ("." ~ path_segment)+ }

// Functions
function = { deletion | only_fields }

deletion = { "del(" ~ target_path ~ ")" }
only_fields = { "only_fields(" ~ target_path ~ ("," ~ target_path)* ~ ")" }

// Queries
path_coalesce = !{ "(" ~ path_segment ~ ("|" ~ path_segment)+ ~ ")" }

dot_path = ${ ("." ~ (path_segment | path_coalesce))+ }

group = { "(" ~ query_arithmetic ~ ")" }

value = _{ string | number | boolean | null }

boolean = { "true" | "false" }

null = { "null" }

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "n" | "t")
    // TODO: Eventually expand this to allow more escape sequences.
    // | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    // | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

number = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

// NOTE: Not including delete here yet, but there's no reason we can't slot it
// in at some point and get `.nested = del(.foo)`.
query_leaf = _{ value | dot_path | group }

// Arithmetic, broken down into tiers in order to support operator precedence.
// Operators of the same tier are resolved from left to right.

// Deepest tier is the highest order of precedence, we call this just 'query' as
// it'll appear in parser error messages.
arithmetic_operator_product = { "*" | "/" | "%" }
query = { query_leaf ~ (arithmetic_operator_product ~ query_leaf)* }

arithmetic_operator_sum = { "+" | "-" }
query_arithmetic_sum = { query ~ (arithmetic_operator_sum ~ query)* }

arithmetic_operator_compare = { ">=" | ">" | "<=" | "<" | "!=" | "==" }
query_arithmetic_compare = { query_arithmetic_sum ~ (arithmetic_operator_compare ~ query_arithmetic_sum)* }

arithmetic_operator_boolean = { "||" | "&&" }
query_arithmetic_boolean = { query_arithmetic_compare ~ (arithmetic_operator_boolean ~ query_arithmetic_compare)* }

query_arithmetic = _{ query_arithmetic_boolean }

WHITESPACE = _{ " " | "\t" }
