mapping = _{ SOI ~ statement ~ (NEWLINE+ ~ statement)* ~ NEWLINE* ~ EOI }

statement = _{ assignment | root_function | if_statement }

assignment = { target_path ~ "=" ~ query_arithmetic }

if_statement = {
    "if" ~ query_arithmetic ~ "{" ~ NEWLINE* ~
        statement ~
    NEWLINE* ~ "}" ~ ( "else" ~ "{" ~ NEWLINE* ~
        statement ~
    NEWLINE* ~ "}" )?
}

// Paths
path_index = ${ "[" ~ ('0'..'9')+ ~ "]" }
path_segment = ${ (ASCII_ALPHA | "_" | "-" )+ ~ path_index? }
quoted_path_segment = ${ "\"" ~ inner_quoted_string ~ "\"" ~ path_index? }

target_path = @{ ("." ~ (path_segment | quoted_path_segment))+ }

// Functions
root_function = {
    deletion |
    only_fields |
    merge
}

deletion = { "del(" ~ target_paths ~ ")" }
only_fields = { "only_fields(" ~ target_paths ~ ")" }
merge = { "merge(" ~ target_path ~ "," ~ query_arithmetic ~ ("," ~ query_arithmetic)? ~ ")" }

// One or more path arguments for a given function.
//
// Can be used to parse functions that take one or more paths, e.g.:
//
// my_func = { "my_func(" ~ target_paths ~ ")" }
// => my_func(.bar, .baz)
//
target_paths = _{ target_path ~ ("," ~ target_path)* }

// Queries
path_coalesce = !{ "(" ~ (path_segment | quoted_path_segment) ~ ("|" ~ (path_segment | quoted_path_segment))+ ~ ")" }

dot_path = ${ ("." ~ (path_segment | quoted_path_segment | path_coalesce))+ }

ident = @{ ASCII_ALPHA_LOWER ~ ( ASCII_ALPHA_LOWER | "_" )* }

function = { ident ~ "(" ~ function_arguments? ~ ")"  }

function_arguments = _{ (function_argument ~ ("," ~ function_argument)* ) }

function_argument = { query_arithmetic | named_argument }

named_argument = { ident ~ "=" ~ query_arithmetic }

group = { "(" ~ query_arithmetic ~ ")" }

value = _{ string | number | boolean | null }

boolean = { "true" | "false" }

null = { "null" }

string = ${ "\"" ~ inner_quoted_string ~ "\"" }
inner_quoted_string = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "n" | "t")
    // TODO: Eventually expand this to allow more escape sequences.
    // | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    // | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

number = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}


not_operator = { "!" ~ query_leaf }

query_leaf = _{ not_operator | value | dot_path | group | function }

// Arithmetic, broken down into tiers in order to support operator precedence.
// Operators of the same tier are resolved from left to right.

// Deepest tier is the highest order of precedence, we call this just 'query' as
// it'll appear in parser error messages.
arithmetic_operator_product = { "*" | "/" | "%" }
query = { query_leaf ~ (arithmetic_operator_product ~ query_leaf)* }

arithmetic_operator_sum = { "+" | "-" }
query_arithmetic_sum = { query ~ (arithmetic_operator_sum ~ query)* }

arithmetic_operator_compare = { ">=" | ">" | "<=" | "<" | "!=" | "==" }
query_arithmetic_compare = { query_arithmetic_sum ~ (arithmetic_operator_compare ~ query_arithmetic_sum)* }

arithmetic_operator_boolean = { "||" | "&&" }
query_arithmetic_boolean = { query_arithmetic_compare ~ (arithmetic_operator_boolean ~ query_arithmetic_compare)* }

query_arithmetic = _{ query_arithmetic_boolean }

WHITESPACE = _{ " " | "\t" }
